apply plugin: 'com.android.library'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    packagingOptions {
        exclude 'lib/**/liblua.so'
        exclude 'lib/**/libcorona.so'
    }
}

dependencies {
    implementation ':Corona@aar'
}

tasks.register("explodeCoronaAAR", Copy) {
    println "$s2dNative/Corona/android/lib/gradle/Corona.aar"
    from(zipTree("$s2dNative/Corona/android/lib/gradle/Corona.aar")) {
        include("jni/**")
    }
    into(layout.buildDirectory.dir("intermediates/coronaJNI"))
}
preBuild.dependsOn explodeCoronaAAR

task extractPluginJar() {
    group "Solar2Dev"
    dependsOn assemble
    doLast {
        copy {
            from {
                zipTree("$buildDir/outputs/aar/${project.name}-release.aar").matching {
                    include 'classes.jar'
                }.singleFile
            }
            into "$buildDir/outputs/"
            String packageName = new XmlSlurper().parse(file('src/main/AndroidManifest.xml')).@package.text()
            rename "classes.jar", "${packageName}.jar"
        }
    }
}
